plugins {
    id 'java'
    id 'application'
}

group 'com.mygame'
version '0.0.7'

mainClassName = "client.Main"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

project.ext {
    jmeVer = '3.6.0-stable'
}

project(":assets") {
    apply plugin: "java"

    buildDir = rootProject.file("build/assets")

    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

dependencies {

    // Core JME
    implementation "org.jmonkeyengine:jme3-core:$jmeVer"
    implementation "org.jmonkeyengine:jme3-desktop:$jmeVer"
    implementation "org.jmonkeyengine:jme3-lwjgl3:$jmeVer"

    // Suppress errors / warnings building in SDK
    implementation "org.jmonkeyengine:jme3-jogg:$jmeVer"
    implementation "org.jmonkeyengine:jme3-plugins:$jmeVer"
  
    // GUI Library
    implementation "org.jmonkeyengine:jme3-niftygui:$jmeVer"
  
    // Networking Library
    implementation "org.jmonkeyengine:jme3-networking:$jmeVer"

    // Additional Libraries
    implementation "org.jmonkeyengine:jme3-effects:$jmeVer"
    implementation "com.github.stephengold:Heart:8.6.0"
    implementation "com.epagagames:particlemonkey:1.1.0"
   // implementation "com.github.polincdev:ShaderBlowEx:master-SNAPSHOT"
    implementation "com.simsilica:zay-es-net:1.5.2"
  
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
	
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    // Assets sub-project
    runtimeOnly project(':assets')
}

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

wrapper {
    gradleVersion = '8.0'
}