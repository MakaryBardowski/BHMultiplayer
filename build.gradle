plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'  // Add the Shadow plugin
    id 'org.beryx.jlink' version '2.26.0'  // Add the JLink plugin
}

group 'com.mygame'
version '0.0.85'

mainClassName = "client.Main"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

project.ext {
    jmeVer = '3.6.0-stable'
}

project(":assets") {
    apply plugin: "java"

    buildDir = rootProject.file("build/assets")

    sourceSets {
        main {
            resources {
                srcDir '.'
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

dependencies {
    implementation "org.jmonkeyengine:jme3-core:$jmeVer"
    implementation "org.jmonkeyengine:jme3-desktop:$jmeVer"
    implementation "org.jmonkeyengine:jme3-lwjgl3:$jmeVer"
    implementation "org.jmonkeyengine:jme3-jogg:$jmeVer"
    implementation "org.jmonkeyengine:jme3-plugins:$jmeVer"
    implementation "org.jmonkeyengine:jme3-niftygui:$jmeVer"
    implementation "org.jmonkeyengine:jme3-networking:$jmeVer"
    implementation "org.jmonkeyengine:jme3-effects:$jmeVer"
    implementation "com.github.stephengold:Heart:8.6.0"
    implementation "com.epagagames:particlemonkey:1.1.0"
    implementation "com.simsilica:lemur:1.16.0"
    implementation 'org.codehaus.groovy:groovy-all:3.0.21'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    runtimeOnly project(':assets')
}

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

wrapper {
    gradleVersion = '8.0'
}

// ShadowJar configuration
shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

// JLink configuration to bundle JRE
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jlinkBasePath = "${buildDir}/jlinkbase"

    launcher {
        name = 'mygame'
    }
    imageDir = file("${buildDir}/image")
    mergedModule {
        additive = true
    }
}

application {
    mainClass.set(mainClassName)
}

// Task to create a distributable zip including the JRE
task distZipJar(type: Zip) {
    dependsOn shadowJar, jlink
    from("$buildDir/image") {
        into("/")
    }
    from(shadowJar.outputs) {
        into("lib")
    }
    archiveFileName = "mygame-${version}.zip"
}
